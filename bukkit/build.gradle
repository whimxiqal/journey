plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    flatDir {
        dirs 'libs'
    }
    maven {
        name = 'jitpack.io'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'essentialsx'
        url = 'https://repo.essentialsx.net/releases/'
    }
}

dependencies {
    implementation project(':common')

    // Tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'com.google.guava:guava:31.0.1-jre'

    // Annotations
    shadow 'org.jetbrains:annotations:22.0.0'
    testImplementation 'org.jetbrains:annotations:22.0.0'

    // Other plugins
    shadow 'com.github.PikaMug:Quests:4.0.7'
    shadow 'com.github.whimc:Portals:2.8.3'

    // Lombok
    shadow 'org.projectlombok:lombok:1.18.22'
    testImplementation('org.projectlombok:lombok:1.18.22')
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

//    implementation 'javax.visrec:visrec-api:1.0.5'
//    implementation 'com.opencsv:opencsv:5.5.2'

    implementation 'org.bstats:bstats-bukkit:3.0.0'

    implementation 'net.whimxiqal.mantle:bukkit:0.0.0'

    implementation "net.kyori:adventure-platform-bukkit:4.1.2"
    implementation 'net.kyori:adventure-text-serializer-legacy:4.11.0'
    implementation 'net.kyori:adventure-text-serializer-plain:4.11.0'

    implementation "dev.triumphteam:triumph-gui:3.1.2"

    // Integrations
    shadow 'net.essentialsx:EssentialsX:2.19.7'

}

shadowJar {
    archiveFileName = "Journey-Spigot-${project.version}.jar"

    dependencies {
        include(dependency('net.whimxiqal.journey:.*'))
        include(dependency('net.whimxiqal.mantle:.*'))
        include(dependency('org.antlr:antlr4-runtime'))
        include(dependency('com.vmware.antlr4-c3:.*'))
        include(dependency('net.kyori:.*'))
        include(dependency('org.bstats:.*'))
        include(dependency('org.apache.httpcomponents:.*'))
        include(dependency('dev.triumphteam:.*'))
    }

    relocate 'net.whimxiqal.mantle', 'net.whimxiqal.journey.libs.mantle'
    relocate 'org.antlr', 'net.whimxiqal.journey.libs.antlr'
    relocate 'com.vmware.antlr4c3', 'net.whimxiqal.journey.libs.c3'
    relocate 'org.stringtemplate', 'net.whimxiqal.journey.libs.stringtemplate'
    relocate 'net.kyori', 'net.whimxiqal.journey.libs.kyori'
    relocate 'org.bstats', 'net.whimxiqal.journey.libs.bstats'
    relocate 'org.apache.http', 'net.whimxiqal.journey.libs.http'
    relocate 'mozilla', 'net.whimxiqal.journey.libs.mozilla'
    relocate 'dev.triumphteam.gui', 'net.whimxiqal.journey.libs.gui'
}

build {
    jar.enabled = false
    dependsOn 'shadowJar'
}

processResources {
    filesMatching('plugin.yml') {
        expand 'version': version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'journey'
            groupId = project.group
            version = project.version

            from components.java
        }
    }
}
