plugins {
    id 'java'
    id 'journey.java-conventions'
    id 'journey-paper-plugin-conventions' apply false
    id 'com.gradleup.shadow' version '8.3.0'
}

configure(journeyPaperPlugin) {
    pluginName = 'Journey-Bukkit'
}

apply plugin: 'journey-paper-plugin-conventions'

repositories {
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    implementation project(':api')
    implementation project(':bukkit-api')
    implementation project(':command')
    implementation project(':common')
    implementation 'net.whimxiqal.mantle:common:0.2.0'
    implementation 'net.whimxiqal.mantle:paper:0.2.0'
    implementation 'io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'dev.triumphteam:triumph-gui:3.1.10'
    shadow 'org.jetbrains:annotations:24.1.0'

    // TODO remove lombok
    shadow 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

build {
    jar.enabled = false
    dependsOn 'shadowJar'
}

def journeyPaperPluginExtension = extensions.getByType(JourneyPaperPluginExtension)
shadowJar {
    archiveFileName = journeyPaperPluginExtension.pluginName.get() + "-${project.version}.jar"

    dependencies {
        include(dependency('net.whimxiqal.journey:.*'))
        include(dependency('net.whimxiqal.mantle:.*'))
        include(dependency('org.antlr:antlr4-runtime'))
        include(dependency('com.vmware.antlr4-c3:.*'))
        include(dependency('net.kyori:adventure.*'))
        include(dependency('net.kyori:adventure-platform.*'))
        include(dependency('net.kyori:adventure-text-serializer-legacy.*'))
        include(dependency('net.kyori:adventure-text-serializer-gson.*'))
        include(dependency('org.bstats:.*'))
        include(dependency('dev.triumphteam:.*'))
        include(dependency('com.zaxxer:.*'))
        include(dependency('org.json:.*'))
        include(dependency('org.spongepowered:.*'))
    }

    relocate 'net.whimxiqal.mantle', 'net.whimxiqal.journey.libs.mantle'
    relocate 'org.antlr', 'net.whimxiqal.journey.libs.antlr'
    relocate 'com.vmware.antlr4c3', 'net.whimxiqal.journey.libs.c3'
    relocate 'org.stringtemplate', 'net.whimxiqal.journey.libs.stringtemplate'
    relocate 'net.kyori.adventure.platform', 'net.whimxiqal.journey.libs.adventure.platform'
    relocate 'net.kyori.adventure.text.serializer.legacy', 'net.whimxiqal.journey.libs.adventure.legacy'
    relocate 'net.kyori.adventure.text.serializer.gson', 'net.whimxiqal.journey.libs.adventure.gson'
    relocate 'org.bstats', 'net.whimxiqal.journey.libs.bstats'
    relocate 'dev.triumphteam.gui', 'net.whimxiqal.journey.libs.gui'
    relocate 'com.zaxxer.hikari', 'net.whimxiqal.journey.libs.hikari'
    relocate 'org.json', 'net.whimxiqal.journey.libs.json'
    relocate 'org.spongepowered', 'net.whimxiqal.journey.libs.spongepowered'
}