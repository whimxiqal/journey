plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'sonatype-snapshots'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name = 'jitpack.io'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'essentialsx'
        url = 'https://repo.essentialsx.net/releases/'
    }
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    implementation project(':common')
    implementation project(':bukkit-api')

    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'

    // Tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'com.google.guava:guava:31.0.1-jre'

    // Annotations
    shadow 'org.jetbrains:annotations:22.0.0'
    testImplementation 'org.jetbrains:annotations:22.0.0'

    // Lombok
    shadow 'org.projectlombok:lombok:1.18.22'
    testImplementation('org.projectlombok:lombok:1.18.22')
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    implementation 'org.bstats:bstats-bukkit:3.0.0'

    implementation 'net.whimxiqal.mantle:paper:0.1.0-SNAPSHOT'

    implementation "net.kyori:adventure-platform-api:4.1.2"

    implementation "dev.triumphteam:triumph-gui:3.1.2"
}

shadowJar {
    archiveFileName = "Journey-Bukkit-${project.version}.jar"

    dependencies {
        include(dependency('net.whimxiqal.journey:.*'))
        include(dependency('net.whimxiqal.mantle:.*'))
        include(dependency('org.antlr:antlr4-runtime'))
        include(dependency('com.vmware.antlr4-c3:.*'))
        include(dependency('net.kyori:adventure-platform.*'))
        include(dependency('org.bstats:.*'))
        include(dependency('dev.triumphteam:.*'))
        include(dependency('com.zaxxer:.*'))
        include(dependency('org.json:.*'))
    }

    relocate 'net.whimxiqal.mantle', 'net.whimxiqal.journey.libs.mantle'
    relocate 'org.antlr', 'net.whimxiqal.journey.libs.antlr'
    relocate 'com.vmware.antlr4c3', 'net.whimxiqal.journey.libs.c3'
    relocate 'org.stringtemplate', 'net.whimxiqal.journey.libs.stringtemplate'
    relocate 'net.kyori.adventure.platform', 'net.whimxiqal.journey.libs.kyori'
    relocate 'org.bstats', 'net.whimxiqal.journey.libs.bstats'
    relocate 'dev.triumphteam.gui', 'net.whimxiqal.journey.libs.gui'
    relocate 'com.zaxxer.hikari', 'net.whimxiqal.journey.libs.hikari'
    relocate 'org.json', 'net.whimxiqal.journey.libs.json'
}

build {
    jar.enabled = false
    dependsOn 'shadowJar'
}

processResources {
    // Rerun this every time to account for changed version
    outputs.upToDateWhen {
        false
    }
    filesMatching('plugin.yml') {
        expand 'version': project.version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'journey'
            groupId = project.group
            version = project.version

            from components.java
        }
    }
}
