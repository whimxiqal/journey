plugins {
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

defaultTasks 'updateLicensesMain', 'build'

static def suffix() {
    def journeyEnv = System.getenv('JOURNEY_ENV')
    int commitsSinceMajorVersion;
    def tagInfo = 'git describe --tags'.execute().text.trim()
    if (!tagInfo.contains('-')) {
        commitsSinceMajorVersion = 0
    } else {
        commitsSinceMajorVersion = Integer.parseInt((String) tagInfo.split("-")[1])
    }
    if (journeyEnv == null || journeyEnv != 'PROD') {
        // Non-production
        boolean prerelease = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim().startsWith('release');
        StringBuilder builder = new StringBuilder();
        if (!prerelease) {
            String numberOfDevCommits = 'git rev-list HEAD --not origin/main --count'.execute().text.trim();
            if (!numberOfDevCommits.isEmpty()) {
                commitsSinceMajorVersion -= Integer.parseInt(numberOfDevCommits);
            }
            builder.append('.').append(commitsSinceMajorVersion);
        }
        builder.append('-')
        if (prerelease) {
            builder.append('prerelease')
        } else {
            builder.append('dev')
        }
        builder.append('-').append((String) 'git rev-parse --short HEAD'.execute().text.trim());
        return builder.toString()
    } else {
        // Production build
        return '.' + commitsSinceMajorVersion;
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'org.cadixdev.licenser'

    project.ext.publish = false
    project.ext.snapshot = true

    project.ext.majorVersion = '1'
    project.ext.minorVersion = '0'

    project.ext.snapshotVersion = project.ext.snapshot ? "-SNAPSHOT" : ""
    project.ext.apiVersion = project.ext.majorVersion + '.' + project.ext.minorVersion
    project.ext.apiFullVersion = project.ext.apiVersion + project.ext.snapshotVersion
    project.ext.fullVersion = project.ext.apiVersion + suffix();
    version = project.ext.fullVersion
    group = 'net.whimxiqal.journey'

    sourceCompatibility = 16
    targetCompatibility = 16

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE.txt'
    }

    license {
        ignoreFailures = true
        header = rootProject.file('LICENSE.txt')
        include '**/*.java'
        newLine = true
    }

    repositories {
        mavenCentral()
        maven {
            name = 'sonatype-snapshots'
            url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    test {
        useJUnitPlatform()
    }
}